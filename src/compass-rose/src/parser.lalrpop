use ast::*;

grammar;

pub resources: Vec<Resource> = <Resource*>;

Resource: Resource = {
    <attrs:Attribute*> <header:ResourceHeader> ";" => Resource {
        attrs, header, members: vec![],
    },
    <attrs:Attribute*> <header:ResourceHeader> "{" <members:ResourceMember*> "}" => Resource {
        attrs, header, members,
    },
};

ResourceHeader: ResourceHeader  = {
    "resource" <ty:Ident> <endpoint:Endpoint?> => ResourceHeader { ty, endpoint },
};

Endpoint: String = "@" <String>;

ResourceMember: ResourceMember = {
    "relation" <Relation> => ResourceMember::Relation(<>),
    "method" <Method> => ResourceMember::Method(<>),
};

Relation: Relation = {
    <attrs:Attribute*> <rel:Ident> <endpoint:Endpoint?> ";" => Relation {
        attrs, rel, endpoint, members: vec![],
    },
    <attrs:Attribute*> <rel:Ident> <endpoint:Endpoint?> "{" <members:RelationMember*> "}" => Relation {
        attrs, rel, endpoint, members,
    },
};

RelationMember: RelationMember = {
    "method" <Method> => RelationMember::Method(<>),
};

Methods: Vec<Method> = ":" <RepeatWith<Method, "+">>;

Method: Method = {
    <attrs:Attribute*> <method:MethodKind> "in" <format:Ident> ";" => Method {
        attrs, method, format,
    }
};

MethodKind: MethodKind = {
    "Get" => MethodKind::Get,
    "Index" => MethodKind::Index,
};

Ident: String = {
    r"[A-Za-z][A-Za-z0-9]*" => String::from(<>)
};

String: String = {
    <s:r#""(?:[^"\\]|\\.)*""#> => {
        let last = s.len() - 1;
        String::from(&s[1..last])
    },
};

Attribute: Attribute = {
    "#" "[" <id: Ident> "(" <idents:RepeatWith<Ident, ",">> ")" "]" => Attribute::Arg(id, idents),
    "#" "[" <Ident> "]" => Attribute::Ident(<>),
};

RepeatWith<T, X>: Vec<T> = {
    <v:(<T> X)*> <e:T?> => match e {
       None => v,
       Some(e) => {
           let mut v = v;
           v.push(e);
           v
       }
   }
};
